\subsection{Task 5: Access Profile}\label{sec:task5}

\begin{note}
For this task, please write your code in the file \texttt{task5.tf}
at the root of the \texttt{workshop/} directory.\\
You will also be editing \texttt{users.tf} and \texttt{users.auto.tfvars}.
\end{note}

Before creating your first project, you will need an access profile.\\

The access profile must contain SSH keys for all the users created in the previous task, \fullref{sec:task4}.
Rather than declare the SSH keys in a separate variable, you will add them to the \texttt{users} variable to
declare them on per user basis.\\

Modify \texttt{users}'s declaration in \texttt{users.tf} to include a list of SSH users per user.
\begin{tf}
variable "users" {
  type = map(object({
    email = string
    role  = string
    ssh_users = list(object({
      name       = string
      public_key = string
    }))
  }))
  description = "List of project users"
  default     = {}
}
\end{tf}

You can now edit \texttt{users.auto.tfvars} and define a list of SSH users for each user.
Considering the previous example of Alice and Bob, here is the same definition of \texttt{users}
with added SSH users.
\begin{tf}
users = {
  "alice" = {
    email = "alice@gmail.com"
    role  = "Manager"
    ssh_users = [
      {
        name       = "alice-work"
        public_key = "ssh-ed25519 AAAATHEQUICKBROWNFOXJUMPEDOVERTHELAZYDOG example"
      },
      {
        name       = "alice-home"
        public_key = "ssh-ed25519 AAAATHEQUICKBROWNFOXJUMPEDOVERTHELAZYDOG example"
      }
    ]
  },
  "bob" = {
    email = "bob@gmail.com"
    role  = "User"
    ssh_users = [
      {
        name       = "bob-laptop"
        public_key = "ssh-ed25519 AAAATHEQUICKBROWNFOXJUMPEDOVERTHELAZYDOG example"
      }
    ]
  },
}
\end{tf}
Add as many SSH users as you wish to the users defined in the previous task.
Of course, you will need to use valid SSH keys. If you do not wish to create your own,
here is a public key value you can use:
\begin{raw}
ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIB/8P0zXmI/Il81+/fnvGrf0X/VyNTrOJ9nQCxBxjc5m taikun
\end{raw}

\begin{warn}
Within one access profile, the names of SSH users must be unique.
\end{warn}

You can now declare the \texttt{taikun\_access\_profile} resource in \texttt{task5.tf}.
You will use \texttt{for\_each} in a slightly different manner as \texttt{ssh\_user} is a nested resource
within the \texttt{access\_profile} resource.
\href{https://intuinewin.github.io/taikun-docs/resources/access_profile.html}{Here}
is the documentation of the access profile resource.\\

The access profile's requirements are:
\begin{itemize}
  \item It should use the DNS servers \texttt{8.8.8.8} and \texttt{1.1.1.1}.
  \item It should use the NTP server \texttt{time.windows.com}.
  \item It should include all the SSH users defined in \texttt{users.auto.tfvars}.
\end{itemize}

\begin{tip}
While reading \href{https://intuinewin.github.io/taikun-docs/resources/access_profile.html}{access profile's schema},
you may notice \texttt{dns\_server} and \texttt{ntp\_server} are of the type \textit{Block List}.\\

Going back to the pizza order example, here is how block lists are used.
Suppose the \texttt{pizza\_order} resource has an argument \texttt{extra\_topping} defined as such:
\begin{quote}
- extra\_topping (Block List, Max: 5) List of extra pizza toppings. (see below for nested schema)\\

Nested Schema for extra\_topping\\
Required:\\
- name (String) Name of the extra topping\\
- quantity (Number) Quantity
\end{quote}
Here is how Alice could add mozzarella di bufala and basil to her amatriciana pizza order.
\begin{tf}
resource "pizza_order" "orders" {
  client = "alice"
  type   = "amatriciana"
  size   = "large"

  extra_topping {
    name = "basil leaves"
    quantity = 8
  }
  extra_topping {
    name = "bufala slices"
    quantity = 4
  }
}
\end{tf}
\end{tip}
\pagebreak

\begin{note}
The code to define the SSH users of the access profile is provided below.
However, if you wish to give it a go yourself first, here is how.\\

To define the \texttt{ssh\_user} argument, you will need to use a
\href{https://www.terraform.io/docs/language/expressions/dynamic-blocks.html}{dynamic block}
with the \texttt{for\_each} keyword.
You will also need to extract the list of all SSH users from the \texttt{users} variable.
\href{https://www.terraform.io/docs/language/expressions/for.html}{for expressions} and
the \href{https://www.terraform.io/docs/language/functions/flatten.html}{flatten function}
will be of use.
\end{note}
\vfill
{\Large{Spoiler ahead!}}
\vfill
\begin{tf}
resource "taikun_access_profile" "..." {

  # ...
  
  dynamic "ssh_user" {
    for_each = flatten([for user in var.users : user.ssh_users])
    content {
      name       = ssh_user.value.name
      public_key = ssh_user.value.public_key
    }
  }
}
\end{tf}

Once you have fully declared the access profile resource, apply your changes and move on to the next task.
